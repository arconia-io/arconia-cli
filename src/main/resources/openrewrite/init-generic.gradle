initscript {
    def pluginVersion = System.getProperty('pluginVersion')
    repositories {
        maven { url "https://plugins.gradle.org/m2" }
    }
    dependencies { classpath("org.openrewrite:plugin:${pluginVersion}") }
}

def recipeLibrary = System.getProperty('recipeLibrary')
def recipeVersion = System.getProperty('recipeVersion')

rootProject {
    plugins.apply(org.openrewrite.gradle.RewritePlugin)
    if (recipeLibrary != null && recipeVersion != null) {
        dependencies {
            rewrite("${recipeLibrary}:${recipeVersion}")
        }
    }
    rewrite {
        exportDatatables = true
    }
    afterEvaluate {
        if (repositories.isEmpty()) {
            repositories {
                mavenCentral()
            }
        }
    }
    configurations {
        rewrite {
            attributes {
                attribute(Usage.USAGE_ATTRIBUTE, objects.named(Usage, Usage.JAVA_RUNTIME))
                attribute(Category.CATEGORY_ATTRIBUTE, objects.named(Category, Category.LIBRARY))
                attribute(LibraryElements.LIBRARY_ELEMENTS_ATTRIBUTE, objects.named(LibraryElements, LibraryElements.JAR))
                attribute(Bundling.BUNDLING_ATTRIBUTE, objects.named(Bundling, Bundling.EXTERNAL))
                attribute(TargetJvmEnvironment.TARGET_JVM_ENVIRONMENT_ATTRIBUTE, objects.named(TargetJvmEnvironment, TargetJvmEnvironment.STANDARD_JVM))
            }
        }
    }
}
