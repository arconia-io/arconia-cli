plugins {
    id 'java'
    id 'distribution'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.graalvm.buildtools.native' version '0.10.3'
    id 'org.cyclonedx.bom' version '1.10.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(22)
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springShellVersion', "3.3.2")
}

dependencies {
    implementation platform("org.springframework.shell:spring-shell-dependencies:${springShellVersion}")

    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.shell:spring-shell-starter'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.shell:spring-shell-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// Application

springBoot {
    buildInfo()
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('cyclonedxBom') {
    includeConfigs = ["runtimeClasspath"]
    outputFormat = "json"
    projectType = "application"
    schemaVersion = "1.6"
}

// Distribution

distributions {
    main {
        contents {
            from("$buildDir/native/nativeCompile") {
                into 'bin'
                include 'arconia-cli', 'arconia-cli.exe'
                rename { filename ->
                    filename.replace("arconia-cli", "arconia")
                }
            }
            from("$buildDir/reports") {
                include 'application.cdx.json'
                rename { 'sbom.cdx.json' }
            }
            from(rootDir) {
                include 'LICENSE', 'README.md'
            }
        }
    }
}

tasks.named('distZip') {
    onlyIf {
        gradle.startParameter.taskNames.contains("distZip")
    }
    archiveFileName = 'arconia-cli.zip'
}

tasks.named('distTar') {
    enabled = false
}
