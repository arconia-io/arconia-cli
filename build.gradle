plugins {
    id 'java'
    id 'distribution'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.graalvm.buildtools.native' version '0.10.6'
    id 'org.cyclonedx.bom' version '2.3.1'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(24)
        nativeImageCapable = true
    }
}

repositories {
    mavenCentral()
}

ext {
    set('jLineVersion', "3.29.0")
    set('jSpecifyVersion', "1.0.0")
    set('springShellVersion', "3.4.0")
}

dependencies {
    implementation platform("org.springframework.shell:spring-shell-dependencies:${springShellVersion}")

    implementation "org.jline:jline-builtins:${jLineVersion}"
    implementation "org.jline:jline-console:${jLineVersion}"
    implementation "org.jline:jline-native:${jLineVersion}"
    implementation "org.jline:jline-reader:${jLineVersion}"
    implementation "org.jline:jline-style:${jLineVersion}"
    implementation "org.jline:jline-terminal:${jLineVersion}"
    implementation "org.jline:jline-terminal-jni:${jLineVersion}"

    implementation "org.jspecify:jspecify:${jSpecifyVersion}"
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.shell:spring-shell-starter'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.shell:spring-shell-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

springBoot {
    buildInfo()
}

graalvmNative {
    binaries {
        main {
            imageName = "arconia"
            // See: https://docs.spring.io/spring-shell/reference/building.html#_ffm
            buildArgs.add('--enable-native-access=ALL-UNNAMED')
        }
    }
}

tasks.withType(JavaExec).configureEach {
   // See: https://docs.spring.io/spring-shell/reference/building.html#_ffm
   jvmArgs += ["--enable-native-access=ALL-UNNAMED"]
}

tasks.withType(Test).configureEach {
   // See: https://docs.spring.io/spring-shell/reference/building.html#_ffm
   jvmArgs += ["--enable-native-access=ALL-UNNAMED"]
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('cyclonedxBom') {
    includeConfigs = ["runtimeClasspath"]
    outputFormat = "json"
    projectType = "application"
    schemaVersion = "1.6"
}

distributions {
    main {
        contents {
            from("$buildDir/native/nativeCompile") {
                into 'bin'
                include 'arconia', 'arconia.exe'
            }
            from("$buildDir/reports") {
                include 'application.cdx.json'
                rename { 'sbom.cdx.json' }
            }
            from(rootDir) {
                include 'LICENSE', 'README.md'
            }
        }
    }
}

tasks.named('distZip') {
    def archiveName = gradle.startParameter.projectProperties['archiveName'] ?: "arconia-${project.version}.zip"
    onlyIf {
        gradle.startParameter.taskNames.contains("distZip")
    }
    archiveFileName = "${archiveName}"
}

tasks.named('distTar') {
    enabled = false
}
